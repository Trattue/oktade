-- |
-- Module: Data.Oktade.Classfile.Class.Methods.AccessFlags
-- License: Apache-2.0
--
-- Types and functions for parsing and unparsing the classfile method access
-- flags.
module Data.Oktade.Classfile.Class.Methods.AccessFlags
  ( -- * Access Flags
    AccessFlags (..),
    AccessFlag (..),
  )
where

import Data.Bits (Bits ((.|.)), (.&.))
import Data.ByteString.Builder (word16BE)
import Data.Oktade.ByteConstant (Word16Constant (..))
import Data.Oktade.ByteParser (anyWord16)
import Data.Oktade.Classfile.Class.Parse (Parse (..), Unparse (..))

--------------------------------------------------------------------------------
-- Access Flags
--------------------------------------------------------------------------------

-- | List of 'AccessFlag's a field has.
newtype AccessFlags = AccessFlags [AccessFlag]
  deriving (Show)

instance Parse AccessFlags where
  parser _ =
    let flags =
          [ Public,
            Private,
            Protected,
            Static,
            Final,
            Synchronized,
            Bridge,
            Varargs,
            Native,
            Abstract,
            Strict,
            Synthetic
          ]
     in AccessFlags <$> do
          mask <- anyWord16
          return $ foldr (prependIfPresent mask) [] flags
    where
      prependIfPresent m f acc
        | m .&. value16 f == value16 f = f : acc
        | otherwise = acc

instance Unparse AccessFlags where
  unparser _ (AccessFlags as) = word16BE $ foldr ((.|.) . value16) 0 as

-- | Represents a single field access flag.
data AccessFlag
  = -- | Declared public, may be accessed from outside its package.
    Public
  | -- | Declared private, accessible only within the defining class and other
    -- classes belonging to the same nest.
    Private
  | -- | Declared public, may be accessed within subclasses.
    Protected
  | -- | Declared static.
    Static
  | -- | Declared final, never directly assigned to after object construction.
    Final
  | -- | Declared synchronized, invocation is wrapped by a monitor use.
    Synchronized
  | -- | Bridge method generated by the compiler.
    Bridge
  | -- | Method has variable number of arguments.
    Varargs
  | -- | Declared native, implemented in a non-JVM language.
    Native
  | -- | Declared abstract, no implementation is provided.
    Abstract
  | -- | Declared strictfp, floating-point mode is FP-strict.
    Strict
  | -- | Declared synthetic, not present in the source code.
    Synthetic
  deriving (Show)

instance Word16Constant AccessFlag where
  value16 Public = 0x0001
  value16 Private = 0x0002
  value16 Protected = 0x0004
  value16 Static = 0x0008
  value16 Final = 0x0010
  value16 Synchronized = 0x0020
  value16 Bridge = 0x0040
  value16 Varargs = 0x0080
  value16 Native = 0x0100
  value16 Abstract = 0x0400
  value16 Strict = 0x0800
  value16 Synthetic = 0x1000
